=== Custom Message Converters

The plugin provides a way for authors to create their own message converters. A custom message converter must be placed
in the `grails-app/rabbit-converters` path, and must end with `Converter.groovy`.

A message converter must implement the appropriate interface for the type of conversion it supports. The
`ByteToObjectConverter` interface is used when a converter supports incoming messages from RabbitMQ, while the
`ObjectToByteConverter` interface is used when a converter supports outgoing messages to RabbitMQ. Classes that support
both should implement both interfaces.

Message converters advertise what objects and MIME types it supports. The conversion system will typically attempt to
first match an incoming message with a converter matching its MIME type, if it was provided. Message converters that are
aware of the MIME types it supports provides the conversion system with a more accurate type detection ability.
Additionally, converters may apply a MIME type to outgoing messages if one has not already been defined.

Below is an example converter for the String object type. Custom converters should follow the same format.

.StringMessageConverter
[source,groovy]
-----
package com.budjb.rabbitmq.converter

import groovy.transform.CompileStatic
import org.springframework.util.MimeType

/**
 * A converter that supports conversion to and from a {@link String}.
 */
@CompileStatic
class StringMessageConverter implements ByteToObjectConverter, ObjectToByteConverter {
    /**
     * Mime type.
     */
    private static final MimeType mimeType = MimeType.valueOf('text/plain')

    /**
     * {@inheritDoc}
     */
    @Override
    boolean supports(Class<?> type) {
        return String.isAssignableFrom(type)
    }

    /**
     * {@inheritDoc}
     */
    @Override
    boolean supports(MimeType mimeType) {
        return mimeType.isCompatibleWith(this.mimeType)
    }

    /**
     * {@inheritDoc}
     */
    @Override
    ByteToObjectResult convert(ByteToObjectInput input) {
        return new ByteToObjectResult(new String(input.getBytes(), input.getCharset()))
    }

    /**
     * {@inheritDoc}
     */
    @Override
    ObjectToByteResult convert(ObjectToByteInput input) {
        return new ObjectToByteResult(
            ((String) input.getObject()).getBytes(input.getCharset()),
            new MimeType(mimeType, input.getCharset())
        )
    }
}
-----
